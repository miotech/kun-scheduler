plugins {
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'java'
    id "io.freefair.lombok"
    id "org.sonarqube"
    id "com.gorylenko.gradle-git-properties" version "2.2.3"
}

repositories {
    mavenCentral()
}

task buildBashOperator (type:Exec){
    workingDir "../../"
    commandLine "./gradlew",":kun-data-platform:kun-workflow-operators:bash-operator:shadowJar"
}

task buildSparkOperator (type:Exec){
    workingDir "../../"
    commandLine "./gradlew",":kun-data-platform:kun-workflow-operators:spark-operators:shadowJar"
}

task copyBashOperator(type: Copy){
    from "../kun-workflow-operators/bash-operator/build/libs/bash-operator-0.1.jar"
    rename {
        "bash-operator.jar"
    }
    into "src/main/resources"
    dependsOn(buildBashOperator)
}
task copySparkOperator(type: Copy){
    from "../kun-workflow-operators/spark-operators/build/libs/spark-operators-0.1.jar"
    rename {
        "spark-operator.jar"
    }
    into "src/main/resources"
    dependsOn(buildSparkOperator)
}

task copySparkSqlOperator(type: Copy){
    from "../kun-workflow-operators/spark-operators/build/libs/spark-operators-0.1.jar"
    rename {
        "sparkSql-operator.jar"
    }
    into "src/main/resources"
    dependsOn(buildSparkOperator)
}

compileJava.dependsOn(copyBashOperator,copySparkOperator,copySparkSqlOperator)

task deleteLocalJar(type: Delete){
    delete "src/main/resources/bash-operator.jar"
    delete "src/main/resources/spark-operator.jar"
    delete "src/main/resources/sparkSql-operator.jar"

}
bootJar.dependsOn(deleteLocalJar)

bootJar {
    mainClassName = 'com.miotech.kun.dataplatform.DataPlatformApplication'
}

dependencies {
    implementation project(':kun-commons:kun-commons-db')
    implementation project(':kun-commons:kun-commons-utils')
    implementation project(':kun-workflow:kun-workflow-core')
    implementation project(':kun-workflow:kun-workflow-client')
    implementation project(':kun-workflow:kun-workflow-utils')
    implementation project(':kun-data-platform:kun-workflow-operators:spark-operators')
    implementation "com.fasterxml.jackson.core:jackson-core"
    implementation "com.fasterxml.jackson.core:jackson-databind"
    implementation project(':kun-app-common')
    implementation project(':kun-security:kun-security-common')
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'com.squareup.okhttp3:okhttp'
    implementation 'org.postgresql:postgresql'
    implementation 'io.springfox:springfox-boot-starter:3.0.0'
    implementation 'com.googlecode.json-simple:json-simple'
    implementation "com.h2database:h2"
    implementation 'redis.clients:jedis'
    implementation "org.flywaydb:flyway-core"
    implementation 'org.apache.commons:commons-email'
    implementation "com.cronutils:cron-utils"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.testcontainers:testcontainers"
    testImplementation 'com.icegreen:greenmail'
    testImplementation 'com.icegreen:greenmail-junit4'
    testImplementation 'com.icegreen:greenmail-spring'
    testImplementation 'org.jooq:joor-java-8'
}

jacocoTestReport {

    afterEvaluate {
        getClassDirectories().setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/**operator.jar'])
        }))
    }
}

test {
    useJUnitPlatform()
}

apply plugin: "org.sonarqube"
sonarqube {
    properties {
        property "sonar.sources", "src/main"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.java.binaries", "build"
    }
}