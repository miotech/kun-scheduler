plugins {
    id 'org.springframework.boot' version '2.2.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id "io.freefair.lombok" version "5.1.0"
    id "org.sonarqube" version "2.7"
}

repositories {
    mavenCentral()
}

task buildBashOperator (type:Exec){
    workingDir "kun-workflow-operators/bash-operator"
    commandLine "gradle","shadowJar"
}

task buildSparkOperator (type:Exec){
    workingDir "kun-workflow-operators/spark-operators"
    commandLine "gradle","shadowJar"
}

task copyBashOperator(type: Copy){
    from "kun-workflow-operators/bash-operator/build/libs/bash-operator-0.1.jar"
    rename {
        "bash-operator.jar"
    }
    into "kun-data-platform-web/src/main/resources"
    dependsOn(buildBashOperator)
}
task copySparkOperator(type: Copy){
    from "kun-workflow-operators/spark-operators/build/libs/spark-operators-0.1.jar"
    rename {
        "spark-operator.jar"
    }
    into "kun-data-platform-web/src/main/resources"
    dependsOn(buildSparkOperator)
}

task copySparkSqlOperator(type: Copy){
    from "kun-workflow-operators/spark-operators/build/libs/spark-operators-0.1.jar"
    rename {
        "sparkSql-operator.jar"
    }
    into "kun-data-platform-web/src/main/resources"
    dependsOn(buildSparkOperator)
}

compileJava.dependsOn(copyBashOperator,copySparkOperator,copySparkSqlOperator)

task deleteLocalJar(type: Delete){
    delete "kun-data-platform-web/src/main/resources/bash-operator.jar"
    delete "kun-data-platform-web/src/main/resources/spark-operator.jar"
    delete "kun-data-platform-web/src/main/resources/sparkSql-operator.jar"

}
bootJar.dependsOn(deleteLocalJar)


dependencies {
    implementation project(':kun-data-platform:kun-data-platform-web')
    implementation 'org.apache.dubbo:dubbo-spring-boot-starter'
    implementation 'redis.clients:jedis:2.9.0'
}

bootJar {
    mainClassName 'com.miotech.kun.dataplatform.DataPlatformApplication'
}

jacocoTestReport {

    afterEvaluate {
        getClassDirectories().setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/**operator.jar'])
        }))
    }
}

test {
    useJUnitPlatform()
}

apply plugin: "org.sonarqube"
sonarqube {
    properties {
        property "sonar.sources", "src/main"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.java.binaries", "build"
    }
}