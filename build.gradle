buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.github.ksoichiro.console.reporter' version '0.6.2'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
    }
}

subprojects {
    test {
        finalizedBy jacocoTestReport
    }

    group = 'com.miotech'
    version = rootProject.version

    plugins.withType(JavaPlugin) {
        sourceCompatibility = JavaVersion.VERSION_1_8

        dependencies {
            constraints {
                implementation "com.google.inject:guice:4.2.3"
                implementation 'com.google.guava:guava:28.2-jre'
                implementation "org.slf4j:slf4j-api:1.7.30"
                implementation "commons-dbutils:commons-dbutils:1.7"
                implementation 'org.apache.commons:commons-lang3:3.10'
                implementation 'org.apache.commons:commons-collections4:4.4'
                implementation "com.zaxxer:HikariCP:3.4.5"
                implementation 'ch.qos.logback:logback-classic:1.2.3'
                implementation "org.yaml:snakeyaml:1.21"
                implementation "com.fasterxml.jackson.core:jackson-core:2.11.0"
                implementation "com.fasterxml.jackson.core:jackson-databind:2.11.0"
                implementation 'com.fasterxml.jackson.core:jackson-annotations:2.11.0'
                implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.11.0'
                implementation "org.eclipse.jetty:jetty-server:9.4.28.v20200408"
                implementation "org.eclipse.jetty:jetty-servlet:9.4.28.v20200408"
                implementation "org.eclipse.jetty:jetty-util:9.4.28.v20200408"
                implementation "org.eclipse.jetty:jetty-webapp:9.4.28.v20200408"
                implementation 'com.typesafe:config:1.4.0'
                implementation 'org.json:json:20190722'
                implementation 'org.elasticsearch.client:elasticsearch-rest-high-level-client:6.6.1'
                implementation 'org.apache.commons:commons-lang3:3.9'
                implementation 'org.postgresql:postgresql:42.2.5'
                implementation 'org.apache.hive:hive-jdbc:2.3.4'
                implementation 'mysql:mysql-connector-java:5.1.46'
                implementation "org.flywaydb:flyway-core:6.4.2"
                implementation "org.apache.curator:curator-recipes:4.2.0"
                implementation "io.prestosql:presto-jdbc:333"
                implementation "org.mongodb:mongo-java-driver:3.6.3"
                implementation "com.amazonaws:aws-java-sdk-glue:1.11.792"
                implementation "com.syncron.amazonaws:simba-athena-jdbc-driver:2.0.2"
                implementation "com.arangodb:arangodb-java-driver:5.0.4"
                implementation "org.apache.kafka:kafka-clients:2.5.0"
                implementation "io.sentry:sentry-logback:1.7.30"
                implementation "com.h2database:h2:1.4.200"
                implementation "com.cronutils:cron-utils:9.0.2"

                testImplementation "junit:junit:4.12"
                testImplementation 'org.apache.httpcomponents:httpclient:4.5.12'
                testImplementation "org.hamcrest:hamcrest:2.2"
                testImplementation "org.hamcrest:hamcrest-library:1.3"
                testImplementation 'com.shazam:shazamcrest:0.11'
                testImplementation "org.mockito:mockito-core:3.3.3"
                testImplementation "org.awaitility:awaitility:4.0.3"
                testImplementation "net.bytebuddy:byte-buddy:1.10.10"
            }

            implementation "com.google.inject:guice"
            implementation "org.slf4j:slf4j-api"
            implementation 'ch.qos.logback:logback-classic'
            implementation 'org.apache.commons:commons-lang3'
            implementation 'org.apache.commons:commons-collections4'

            testImplementation "junit:junit"
            testImplementation "org.hamcrest:hamcrest"
            testImplementation "org.hamcrest:hamcrest-library"
            testImplementation 'com.shazam:shazamcrest'
            testImplementation "org.mockito:mockito-core"
            testImplementation "org.awaitility:awaitility"
        }
    }
}

// Referenced from: https://gist.github.com/aalmiray/e6f54aa4b3803be0bcac#gistcomment-3207693
task codeCoverageReport(type: JacocoReport) {
    // Gather execution data from all subprojects
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    // Add all relevant sourcesets from the subprojects
    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}
